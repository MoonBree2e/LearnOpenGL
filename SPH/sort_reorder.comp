#version 460 core
layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

struct Particle {
    vec3 position;
    vec3 velocity;
    float pressure;
    float density;
};

layout(std430, binding = 0) restrict readonly buffer Particles1 {
    Particle inParticles[];
};

layout(std430, binding = 1) restrict writeonly buffer Particles2 {
    Particle outParticles[];
};

layout(std430, binding = 2) buffer Counts {
    uint counts[];
};

layout(std430, binding = 3) restrict readonly buffer Offsets {
    uint offsets[];
};

uniform float spacing;
uniform int particleTotalNum;
uniform ivec3 gridRes;

void main() {
    const uint particleID = gl_GlobalInvocationID.x;
    if (particleID >= particleTotalNum) return;

    const Particle p = inParticles[particleID];
    const ivec3 pGridIndex = clamp(ivec3(p.position / spacing), ivec3(0), gridRes -1);
    const uint index = pGridIndex.z * gridRes.y * gridRes.x + pGridIndex.y * gridRes.x + pGridIndex.x;
    const uint globalOffset = offsets[index];
    const uint localOffset = atomicAdd(counts[index], 1);
    const uint globalIndex = globalOffset + localOffset;
    outParticles[globalIndex] = p;
}