#version 460 core
layout (local_size_x = 1024) in;
layout (binding = 0) buffer block1
{
	float data[];
};



shared float shared_data[gl_WorkGroupSize.x * 2];
void main()
{
	uint id = gl_LocalInvocationID.x;
	uint rd_id;
	uint wr_id;
	uint mask;
	const uint steps = uint(log2(gl_WorkGroupSize.x)) + 1;
	uint step = 0;

	shared_data[id * 2] = data[id * 2];
	shared_data[id * 2 + 1] = data[id * 2 + 1];

	barrier();
	memoryBarrierShared();

	for (step = 0; step < steps; step++)
	{

		mask = (1 << step) - 1;
		rd_id = ((id >> step) << (step + 1)) + mask;
		wr_id = rd_id + 1 + (id & mask);
		shared_data[wr_id] += shared_data[rd_id];

		barrier();
		memoryBarrierShared();
	}
	data[id * 2] = shared_data[id * 2];
	data[id * 2 + 1] = shared_data[id * 2 + 1];

}